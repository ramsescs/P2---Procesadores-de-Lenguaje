S ::= <sentencias>

<sentencias> ::= <sentencia> <comentario> <fin_parrafo> <sentencias> | lambda

<sentencia> ::= <asignacion> | <def_funcion> | <uso_funcion> | <condicional> | <bucle> | <declaracion> | <comentario> | lambda

<comentario> ::= '%' TEXTO | lambda

<declaracion> ::= <tipo> ID '=' <expresion> | <tipo> <varios_id> | <declaracion_reg> | <declaracion_vec>

<varios_id> ::= ID ',' <nombres> | ID

<tipo> ::= entero | real | booleano | caracter

<declaracion_reg> ::= REGISTRO ID '{' <blq_registro> '}'

<blq_registro> ::= <declaracion> ';' <blq_registro> | <declaracion> ';'

<declaracion_vec> ::= VECTOR '<' <tipo> '>' ID '[' EXPRESION ']'

<asignacion> ::= ID '=' <expresion> | ID '.' ID '=' <expresion> | ID '[' <expresion> ']' = <expresion>

<def_funcion> ::= FUNCION ID '(' <lista_args> ')' ':' <tipo> '{' <sentencias_func> '}'

<sentencias_func> ::= <sentencias> DEVOLVER ID

<lista_args> ::= <tipo> ID ',' <lista_args> | <tipo> ID | lambda

<uso_funcion> ::= ID '(' <lista_expresiones> ')'

<lista_expresiones> ::= <expresion> ',' <lista_expresiones> | <expresion> | lambda

<expresion> ::= <expresion_and> <op_or> <expresion> | <expresion_and>

<expresion_and> ::= <expresion_comp> <op_and> <expresion_and> | <expresion_comp>

<expresion_comp> ::= <expresion_add> <op_comp> <expresion_comp> | <expresion_add>

<expresion_add> ::= <expresion_mult> <op_add> <expresion_add> | <expresion_mult> 

<expresion_mult> ::= <factor> <op_mult> <expresion_mult> | <factor>

<factor> ::= '(' <expresion> ')' | <valor> | <uso_funcion> | <operador_unario> <factor>

<op_add> ::= '+' | '-'

<op_mult> ::= '*' | '/'

<op_comp> ::= '==' | '>' | '<' | '>=' | '<='

<operador_unario> ::= '-' | NOT

<valor> ::= NUMERO_ENTERO | NUMERO_REAL | CIERTO | FALSO | CARACTER | ID | ID '.' ID | ID '.' long | ID '[' expresion ']'

<condicional> ::= SI <expresion> ENTONCES <sentencias> SINO <sentencias> FINSI | SI <expresion> ENTONCES <sentencias> FINSI

<bucle> ::= MIENTRAS <expresion> <sentencias> FINMIENTRAS

